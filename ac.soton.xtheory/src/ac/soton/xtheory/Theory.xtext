grammar ac.soton.xtheory.Theory with org.eclipse.xtext.common.Terminals hidden (WS, ML_COMMENT, SL_COMMENT)

//generate theory "http://www.soton.ac/xtheory/Theory"
//import "http://emf.eventb.org/models/core/2014" as eventbcore
import "http://soton.ac.uk/models/eventb/theoryextension/2018"
//import "platform:/ressource/ac.soton.eventb.emf.core.theoryextension/model/eventbcoretheoryextension.ecore"

Theory:
	'theory' name=ID ('(' parameters+=Parameter+')')?
	('import' imports +=ID (imports += ID)*)?
	(
		('type' internalElements += Type) |
		('op' internalElements += Operator) |
		('axm' internalElements += Axiom) |
		('thm' internalElements += Theorem) |
		('ruleblock' ruleBlocks += RuleBlock)
	)*
	'end'
;

Parameter:
	name = ID	
;

Type:
	name=ID
	(
		'(' parameters += [Parameter]+')'
	)?
	'=' constructors += Constructor ('|' constructors += Constructor)*
;

Constructor:
	name=ID
	('(' destructors += Destructor (',' destructors += Destructor)* ')')?	
;

Destructor:
	name=ID ":" type=STRING
;

Operator:
	name=ID ('(' arguments += Argument (',' arguments += Argument)*')')?
	(
		(':' type=STRING)? &
		(notation = Notation)? &
		(associative ?= 'associative' commutative ?= 'commutative'?)?
	)
	('#' precedent=INT)?
	('for' wd=STRING)?
	(
		'=' 
		(
			definition=STRING
		|
			'case' case=ID (cases += Case+)
		)		
	)?
;

Argument:
	name=ID ":" type=STRING
;

enum Notation:
  PREFIX='prefix' | INFIX='infix' | POSTFIX='postfix';

Case:
	pattern=STRING "=>" formula=STRING
;

Axiom:
	'@'name=ID':' predicate=STRING
;

Theorem:
	'@'name=ID':' predicate=STRING
;

RuleBlock:
	('variables' variables += Variable+)?
	('rule' rules += Rule)+
;

Rule:
	'@'name=ID':'
	(
		rrule=RewriteRule |
		irule=InferenceRule
	)
;

Variable:
	name=ID ':' type=STRING
;

RewriteRule:
	pattern=STRING '=='
	(
		urule = UnconditionalRewrite |	
		crule = ConditionalRewrite
	)
;

InferenceRule:
	given+=Given* '|-' infer=Infer
;

Given:
	expression=STRING (required?='required')?
;

Infer:
	expression=STRING
;

UnconditionalRewrite: 
	rhs += STRING
;


ConditionalRewrite:
	rewrites += ConditionalRewriteRule
	(rewrites += ConditionalRewriteRule)*	
;

ConditionalRewriteRule:
	lhs = STRING '=>' rhs = STRING
;